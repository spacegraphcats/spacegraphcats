#
# Snakemake configuration file for running spacegraphcats pipelines.
#
# see script 'run' in this directory for a convenient entry point.
#
# Quickstart: `conf/run dory-test searchquick`
#
from os.path import join

## pull values in from config file:

# name of catlas
catlas_base = config['catlas_base']

# sequence files to use when building catlas
input_sequences = config['input_sequences']

# k-mer size to use for everything
ksize = config['ksize']

# radius for catlas building
radius = config['radius']

# search overhead
overhead = config.get('overhead', 0.0)

# paper evaluation: do not expand the cDBG nodes using the domset
cdbg_only = ""
catlas_suffix = ""
if config.get('cdbg_only', False):
   cdbg_only = "--cdbg-only"
   catlas_suffix = "_cdbg"

# suffix to add to search output
experiment = config.get('experiment', '')
search_out_suffix = ''
if experiment:
    search_out_suffix = '_' + experiment

# remove pendants from bcalm graph? default YES
remove_pendants = ""
if config.get('keep_graph_pendants', False):
    remove_pendants = "-P"

### build some variables for internal use

catlas_dir = '{}_k{}_r{}{}'.format(catlas_base, ksize, radius, catlas_suffix)
search_dir = '{}_search_oh{}{}'.format(catlas_dir, int(overhead*100), search_out_suffix)

# internal definitions for convenience:
python=sys.executable  # define as the version of Python running snakemake

onsuccess:
    import os
    print('\n-------- DONE --------\n')
    if os.path.exists(catlas_dir):
        print('catlas output directory: {}'.format(catlas_dir))
        if os.path.exists(search_dir):
            print('search output directory: {}'.format(search_dir))
        print('')


###############################################################################

### rules!

# build catlas needed for search
rule build:
    input:
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.mphf", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.indices", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.info.csv", catlas_dir=catlas_dir),

rule clean:
    shell:
        "rm -fr {catlas_base} {catlas_dir} {search_dir}"

# build cDBG using bcalm
rule bcalm_cdbg:
     input:
        "bcalm.{catlas_base}.k{ksize}.inputlist.txt"
     output:
        "{catlas_base}/bcalm.{catlas_base}.k{ksize}.unitigs.fa"
     shell:
        "bcalm -in bcalm.{catlas_base}.k{ksize}.inputlist.txt -out {catlas_base}/bcalm.{catlas_base}.k{wildcards.ksize} -kmer-size {wildcards.ksize} -abundance-min 1 >& {output}.log.txt && rm -f {catlas_base}/bcalm.{catlas_base}.k{wildcards.ksize}.{{h5,unitigs.fa.glue*}}"

# create list of input files for bcalm
rule bcalm_cdbg_inpfiles:
     input:
        input_sequences
     output:
        "bcalm.{catlas_base}.k{ksize}.inputlist.txt"
     run:
        with open("bcalm.{catlas_base}.k{ksize}.inputlist.txt".format(catlas_base=catlas_base, ksize=ksize), 'wt') as fp:
            for name in input_sequences:
                fp.write('{}\n'.format(name))

# build catlas input from bcalm output by reformatting
rule bcalm_catlas_input:
     input:
        expand("{catlas_base}/bcalm.{catlas_base}.k{ksize}.unitigs.fa", ksize=ksize, catlas_base=catlas_base)
     output:
        join(catlas_dir, "cdbg.gxt"),
        join(catlas_dir, "contigs.fa.gz"),
        join(catlas_dir, "contigs.fa.gz.info.csv")
     shell:
        "{python} -m spacegraphcats.cdbg.bcalm_to_gxt {remove_pendants} -k {ksize} {input} {catlas_dir}/cdbg.gxt {catlas_dir}/contigs.fa.gz"

rule reads_bgzf:
     input:
        input_sequences
     output:
        "{catlas_base}/{catlas_base}.reads.bgz"
     shell:
        "{python} -m spacegraphcats.utils.make_bgzf {input} -o {output}"


# label the reads by contig
rule label_reads:
     input:
        expand("{catlas_base}/{catlas_base}.reads.bgz", catlas_base=catlas_base),
        #expand("{catlas_dir}/contigs.fa.gz", catlas_dir=catlas_dir)
     output:
        join(catlas_dir, "reads.bgz.labels")
     shell:
        "{python} -m spacegraphcats.cdbg.label_cdbg {catlas_dir} {input} {output}"


# build catlas!
rule build_catlas:
     input:
        join(catlas_dir, "cdbg.gxt"),
        join(catlas_dir, "contigs.fa.gz"),
     output:
        join(catlas_dir, "first_doms.txt"),
        join(catlas_dir, "catlas.csv"),
        join(catlas_dir, "commands.log")
     shell:
        "{python} -m spacegraphcats.catlas.catlas --no_checkpoint {catlas_dir} {radius}"

# index contigs, count node sizes
rule make_contigs_kmer_index:
     input:
        join(catlas_dir, "contigs.fa.gz")
     output:
        join(catlas_dir, "contigs.fa.gz.mphf"),
        join(catlas_dir, "contigs.fa.gz.indices"),
     shell:
        "{python} -m spacegraphcats.index.index_contigs_by_kmer -k {ksize} {catlas_dir}"

### Search rules.

def make_query_base(searchfiles):
    x = []
    for filename in searchfiles:
        x.append("{}/{}.contigs.sig".format(search_dir, os.path.basename(filename)))
        x.append("{}/{}.cdbg_ids.txt.gz".format(search_dir, os.path.basename(filename)))
        x.append("{}/{}.frontier.txt.gz".format(search_dir, os.path.basename(filename)))
    return x

# do a quick search!
rule searchquick:
    input:
        config['searchquick'],
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.mphf", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.indices", catlas_dir=catlas_dir)
    output:
        expand("{search_dir}/results.csv", search_dir=search_dir),
        make_query_base(config['searchquick']),
    shell:
        "{python} -m spacegraphcats.search.extract_nodes_by_query {catlas_dir} {search_dir} --query {config[searchquick]} -k {ksize} --overhead={overhead} {cdbg_only}"


# do a full search!
rule search:
    input:
        config['search'],
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.mphf", catlas_dir=catlas_dir),
        expand("{catlas_dir}/contigs.fa.gz.indices", catlas_dir=catlas_dir)
    output:
        expand("{search_dir}/results.csv", search_dir=search_dir),
        make_query_base(config['search']),
    shell:
        "{python} -m spacegraphcats.search.extract_nodes_by_query {catlas_dir} {search_dir} --query {config[search]} -k {ksize} --overhead={overhead} {cdbg_only}"

ruleorder: search > searchquick

### Extract contigs and reads.

def make_extract_contigs_base(searchfiles):
    x = []
    for filename in searchfiles:
        x.append("{}/{}.cdbg_ids.contigs.fa.gz".format(search_dir, os.path.basename(filename)))
    return x

def make_extract_reads_base(searchfiles):
    x = []
    for filename in searchfiles:
        x.append("{}/{}.cdbg_ids.reads.fa.gz".format(search_dir, os.path.basename(filename)))
    return x

# get contigs for a single query
rule extract_contigs_single_file:
    input:
        expand("{catlas_dir}/contigs.fa.gz", catlas_dir=catlas_dir),
        expand("{search_dir}/{{queryname}}.cdbg_ids.txt.gz", search_dir=search_dir)
    output:
        join(search_dir, "{queryname}.cdbg_ids.contigs.fa.gz")
    shell:
        "{python} -m spacegraphcats.search.extract_contigs {catlas_dir} {input[1]} -o {output}"

# get reads for a single query
rule extract_reads_single_file:
    input:
        expand("{catlas_base}/{catlas_base}.reads.bgz", catlas_base=catlas_base),
        expand("{catlas_dir}/reads.bgz.labels", catlas_dir=catlas_dir),
        expand("{search_dir}/{{queryname}}.cdbg_ids.txt.gz", search_dir=search_dir)
    output:
        join(search_dir, "{queryname}.cdbg_ids.reads.fa.gz")
    shell:
        "{python} -m spacegraphcats.search.extract_reads {input[0]} {input[1]} {input[2]} -o {output}"

# get all the reads
rule extract_reads:
    input:
       make_extract_reads_base(config['search'])

# get all the reads
rule extract_contigs:
    input:
       make_extract_contigs_base(config['search'])

### shadow ratio stuff

rule shadow_ratio:
    input:
        expand("{catlas_dir}.shadow.{maxsize}.fa",
               catlas_dir=catlas_dir,
               maxsize=config.get('shadow_ratio_maxsize', 1000))

rule extract_by_shadow_ratio_rule:
    input:
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
    output:
        catlas_dir + ".shadow.{shadow_ratio_maxsize}.fa"
    shell:
        "{python} -m spacegraphcats.search.extract_nodes_by_shadow_ratio --maxsize={wildcards.shadow_ratio_maxsize} {catlas_dir} {output}"
